PREFIX :<http://onto.fel.cvut.cz/ontologies/dataset-descriptor/s-p-o-summary/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX ld-cube: <http://purl.org/linked-data/cube#>
PREFIX ddo: <http://onto.fel.cvut.cz/ontologies/dataset-descriptor/>
PREFIX enh-spo-sum-desc: <http://onto.fel.cvut.cz/ontologies/dataset-descriptor/spo-summary-with-marginals/model/>

CONSTRUCT {
    ?sRepresentant enh-spo-sum-desc:hasDatasetSnapshot ?sTypeDatasetSnapshot .
    ?oRepresentant enh-spo-sum-desc:hasDatasetSnapshot ?oTypeDatasetSnapshot .
}
WHERE {
      {
        BIND(COALESCE(?executionId, STRUUID()) as ?localExecutionId)
      }
      {
        SELECT ?p ?sType ?oType ?sTypeDatasetSnapshot ?oTypeDatasetSnapshot
         {
              ?s ?p ?o.
              FILTER(?p not in ( rdf:subject, rdf:predicate, rdf:object, :hasWeight, rdf:type, :subject, :object, :predicate, enh-spo-sum-desc:hasDatasetSource, enh-spo-sum-desc:hasDatasetSnapshot ))

              OPTIONAL {
                ?s a ?sTypeConcrete .
                FILTER(?sTypeConcrete != owl:Thing)
              }
              OPTIONAL {
                ?o a ?oTypeConcrete .
                FILTER(?oTypeConcrete != owl:Thing)
              }
              OPTIONAL {
                              ?s enh-spo-sum-desc:hasDatasetSnapshot ?sTypeDatasetSnapshot .
                            }
              OPTIONAL {
                              ?o enh-spo-sum-desc:hasDatasetSnapshot ?oTypeDatasetSnapshot .
                            }
              BIND(COALESCE(?sTypeConcrete, owl:Thing) as ?sType)
              BIND(COALESCE(?oTypeConcrete, owl:Thing) as ?oType)

         }
         GROUP BY ?p ?sType ?oType ?sTypeDatasetSnapshot ?oTypeDatasetSnapshot
      }

      BIND(MD5(CONCAT(str(?p),str(?sType),str(?oType),?localExecutionId)) as ?patternHash)
      BIND(IRI(CONCAT("http://onto.fel.cvut.cz/ontologies/dataset-descriptor/spo-summary-descriptor/di-", ?patternHash)) as ?di)
      BIND(IRI(CONCAT(str(?di), "/subject")) as ?sRepresentant)
      BIND(IRI(CONCAT(str(?di), "/object")) as ?oRepresentant)
}
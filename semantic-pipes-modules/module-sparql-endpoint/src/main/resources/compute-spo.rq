PREFIX :<http://onto.fel.cvut.cz/ontologies/dataset-descriptor/s-p-o-summary/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX ld-cube: <http://purl.org/linked-data/cube#>
PREFIX ddo: <http://onto.fel.cvut.cz/ontologies/dataset-descriptor/>

CONSTRUCT{
 ?di rdf:subject _:s;
    rdf:predicate ?p;
    rdf:object _:v.
    _:s a ?sType.
    _:v a ?oType.
    ?di :hasWeight ?w.
}
WHERE {
      {
        BIND(STRUUID() as ?executionId)
      }
      {
        SELECT ?p ?sType ?oType (COUNT(*) as ?w)
         {
              ?s ?p ?o.

              OPTIONAL {
                ?s a ?sTypeConcrete .
                FILTER(?sTypeConcrete != owl:Thing)
              }
              OPTIONAL {
                ?o a ?oTypeConcrete .
                FILTER(?oTypeConcrete != owl:Thing)
              }
              BIND(COALESCE(?sTypeConcrete, owl:Thing) as ?sType)
              BIND(COALESCE(?oTypeConcrete, owl:Thing) as ?oType)

              FILTER(?p not in ( rdf:subject, rdf:predicate, rdf:object, :hasWeight, rdf:type, :subject, :object, :predicate ))
         }
         GROUP BY ?p ?sType ?oType
      }
      BIND(IRI(CONCAT("http://onto.fel.cvut.cz/ontologies/dataset-descriptor/spo-summary-descriptor/di-",MD5(CONCAT(str(?p),str(?sType),str(?oType),?executionId)))) as ?di) .
}
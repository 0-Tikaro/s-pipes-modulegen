PREFIX :<http://onto.fel.cvut.cz/ontologies/dataset-descriptor/s-p-o-summary/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX ld-cube: <http://purl.org/linked-data/cube#>
PREFIX ddo: <http://onto.fel.cvut.cz/ontologies/dataset-descriptor/>
PREFIX enh-spo-sum-desc: <http://onto.fel.cvut.cz/ontologies/dataset-descriptor/spo-summary-with-marginals/model/>

CONSTRUCT{
 ?di rdf:subject ?sRepresentant ;
    rdf:predicate ?p ;
    rdf:object ?oRepresentant ;
        :hasWeight ?w.
    ?sRepresentant a ?sType ;
         enh-spo-sum-desc:hasDatasetSource ?sTypeDatasetSource .
    ?oRepresentant a ?oType ;
          enh-spo-sum-desc:hasDatasetSource ?oTypeDatasetSource .
}
WHERE {
      {
        BIND(STRUUID() as ?executionId)
      }
      {
        SELECT ?p ?sType ?oType (COUNT(*) as ?w)
         {
              ?s ?p ?o.

              OPTIONAL {
                ?s a ?sTypeConcrete .
                FILTER(?sTypeConcrete != owl:Thing)
              }
              OPTIONAL {
                ?o a ?oTypeConcrete .
                FILTER(?oTypeConcrete != owl:Thing)
              }
              BIND(COALESCE(?sTypeConcrete, owl:Thing) as ?sType)
              BIND(COALESCE(?oTypeConcrete, owl:Thing) as ?oType)

              FILTER(?p not in ( rdf:subject, rdf:predicate, rdf:object, :hasWeight, rdf:type, :subject, :object, :predicate, enh-spo-sum-desc:hasDatasetSource ))
         }
         GROUP BY ?p ?sType ?oType
      }

      {
        SELECT ?p2 ?sType2 ?oType2 ?sTypeDatasetSource ?oTypeDatasetSource
         {
              ?s ?p2 ?o.

              OPTIONAL {
                ?s enh-spo-sum-desc:hasDatasetSource ?sTypeDatasetSource .
              }
              OPTIONAL {
                ?o enh-spo-sum-desc:hasDatasetSource ?oTypeDatasetSource .
              }

              OPTIONAL {
                ?s a ?sTypeConcrete .
                FILTER(?sTypeConcrete != owl:Thing)
              }
              OPTIONAL {
                ?o a ?oTypeConcrete .
                FILTER(?oTypeConcrete != owl:Thing)
              }
              BIND(COALESCE(?sTypeConcrete, owl:Thing) as ?sType2)
              BIND(COALESCE(?oTypeConcrete, owl:Thing) as ?oType2)

              FILTER(?p2 not in ( rdf:subject, rdf:predicate, rdf:object, :hasWeight, rdf:type, :subject, :object, :predicate, enh-spo-sum-desc:hasDatasetSource ))
         }
      }

      FILTER(?p = ?p2)
      FILTER(?sType = ?sType2)
      FILTER(?oType = ?oType2)

      BIND(MD5(CONCAT(str(?p),str(?sType),str(?oType),?executionId)) as ?patternHash)
      BIND(IRI(CONCAT("http://onto.fel.cvut.cz/ontologies/dataset-descriptor/spo-summary-descriptor/di-", ?patternHash)) as ?di)
      BIND(IRI(CONCAT(str(?di), "/subject")) as ?sRepresentant)
      BIND(IRI(CONCAT(str(?di), "/object")) as ?oRepresentant)
}